<?prg
#include "lib/tweb/tweb.ch" 

    LOCAL o, oWeb, oBrw, aOptions, hCredentials

	LOCAL cCss := 'tabulator_simple.min.css'	//'tabulator_modern.css'
	LOCAL aData := {;
		{ 'field' => 'Name'		, 'type' => 'Character', 'len' => 30, 'dec' => 0 },;
		{ 'field' => 'First'	, 'type' => 'Character', 'len' => 20, 'dec' => 0 },;
		{ 'field' => 'Age'		, 'type' => 'Numeric'  , 'len' =>  3, 'dec' => 0 },;
		{ 'field' => 'Hiredate'	, 'type' => 'Date'	   , 'len' =>  8, 'dec' => 0 };
	}	
	LOCAL aRepo 		:= AppGetRepo()
	LOCAL aTypes 		:= { 'Character', 'Numeric', 'Logic', 'Date', 'Memo' }
	LOCAL aTypesKey 	:= { 'C'        , 'N'      , 'L'    , 'D'   , 'M' }
	LOCAL hCfg 			:= {=>}
	
	if ! USessionReady()
		URedirect( 'login' )
		retu nil
	endif	
	
	hCredentials := USession( 'credentials' )
	
	DEFINE WEB oWeb TITLE 'U-Dbu' 
	
		oWeb:AddCss( 'https://unpkg.com/tabulator-tables@5.4.3/dist/css/' + cCss )
		
		HTML oWeb FILE 'dbu/view/menu.html' PARAMS oWeb, 'Structure', hCredentials

		DEFINE FORM o ID 'struct' API 'api_structure' OF oWeb 			
			o:cSizing  := 'sm'
			o:lDessign := .f.
		
		HTML o 
			<style>

				.myscreen { 					
					max-width: 600px;
				}	

			</style>		
		ENDTEXT
		
		HTML o FILE 'dbu/view/crumb.html' PARAMS o, 'Table structure' 

		INIT FORM o 
			
			ROW o HALIGN 'center' CLASS 'pt-1' 					
								
				COL o GRID 10 CLASS 'myscreen'	
				
					ROWGROUP o 
					
						//	BarMenu with buttons
						
							hCfg[ 'left' ]  := {;
											{ 'label' => '&nbsp;New Dbf'   , 'icon' => '<i class="fa fa-file" aria-hidden="true"></i>', 'action' => 'new_dbf', 'confirm' => 'Create new table?' },;
											{ 'label' => '&nbsp;Select Dbf', 'icon' => '<i class="fa fa-table" aria-hidden="true"></i>', 'action' => 'dlg_select_dbf'  };
											   } 
											  
							hCfg[ 'right' ] := {;
											{ 'label' => '&nbsp;Create'   , 'icon' => '<i class="fa fa-cog" aria-hidden="true"></i>', 'action' => 'create_dbf'		, 'pbs' => 'InitProc' };
											   } 									  
						//	This line is a view. Explanations in view. Level advanced
						
							HTML o FILE 'dbu/view/mybar.html' PARAMS o, hCfg											
					
					ENDROW o 	
				
					ROWGROUP o VALIGN 'bottom' 						
						
						SELECT oSelect  ID 'repo' LABEL 'Repository' PROMPT aRepo GRID 4 OF o				
			
						GET ID 'file' VALUE '' LABEL 'File'  GRID 8 ;
							BUTTON '<i class="fa fa-info-circle" aria-hidden="true"></i>&nbsp;Info' ACTION 'info' OF o												
						
					ENDROW o 						

			
					ROWGROUP o VALIGN 'bottom'
						GET ID 'field' VALUE '' LABEL 'Field' GRID 4 ;
							BUTTON '<i class="fa fa-plus" aria-hidden="true"></i>' ACTION 'add' OF o
						
						SELECT  ID 'type' LABEL 'Type' PROMPT aTypes  VALUES aTypesKey GRID 4  ONCHANGE 'type' OF o
						GET ID 'len'   VALUE '8' LABEL 'Len'  ALIGN 'center' TYPE 'number' GRID 2 OF o
						GET ID 'dec'   VALUE '0' LABEL 'Dec' ALIGN 'center' TYPE 'number'  GRID 2 OF o

					ENDROW o 
					
					ROW o   CLASS "pl-3 pr-3"
				
						aOptions := { ;
							'index' => 'field',   		;		// Default id
							'height' => '300px',	;								
							'movableRows' => .t.,	;
							'selectable' => 1, 				;	// Podem posar 1 (sols 1 col)
							'selectableRollingSelection' => 1,;
							'selectableRangeMode' => "click"	;								
						}															
						
						DEFINE BROWSE oBrw ID 'brwstr' OPTIONS aOptions ;
							CLASS 'w-100' ;
							TITLE '<h5>Structure</h5>' ALL OF o 
							
							//	https://tabulator.info/docs/5.4/columns 
							//	https://tabulator.info/docs/5.4/select#setup-formatting
							COL oCol TO oBrw CONFIG { 'formatter' => "rowSelection",  'align' => "center", 'headerSort' => .F., 'hozAlign' => "center" }
													
							COL oCol TO oBrw CONFIG { 'rowHandle' => .t., 'formatter' => "handle", 'headerSort' => .f., 'frozen' => .t., 'width' => 30, 'minWidth' => 30 }
							
							COL oCol TO oBrw CONFIG { 'title' => "Field", 'field' => "field", 'width' => 200, 'headerSort' => .f. }
							COL oCol TO oBrw CONFIG { 'title' => "Type"	, 'field' => "type" , 'headerSort' => .f. }
							COL oCol TO oBrw CONFIG { 'title' => "Len"	, 'field' => "len", 'hozAlign' => "center", 'headerSort' => .f.  }
							COL oCol TO oBrw CONFIG { 'title' => "Dec"	, 'field' => "dec", 'hozAlign' => "center", 'headerSort' => .f.}
							
						INIT BROWSE oBrw 
			
					ENDROW o 
					
					ROWGROUP o CLASS "pl-3 pr-3"
					
						//	Aqui podriamos usar la view de la barra superior, pero para que 
						//	se pueda ver la diferencia de codigo...y mantenimiento.
						//
						//	Here we could use the top bar view, but for what
						//  you can see the code difference... and maintenance.
						
						
						HTML o INLINE '<div class="mybar pt-1 pb-1">'
							COL o GRID 12 							
								HTML o INLINE '<div class="btn-group">' 
									BUTTON LABEL '&nbsp;Edit' ICON '<i class="fa fa-check" aria-hidden="true"></i>' ACTION 'edit' GRID 0 WIDTH '100px'  CLASS 'btn btn-outline-dark' OF o
									BUTTON LABEL '&nbsp;Del'  ICON '<i class="fa fa-trash" aria-hidden="true"></i>' ACTION 'del' GRID 0 WIDTH '100px' CLASS 'btn btn-outline-dark'  OF o
								HTML o INLINE '</div>'
							ENDCOL o
						HTML o INLINE '</div>'
						
					ENDROW o 						
	
				ENDCOL o	
				
			ENDROW o		
			
			HTML o 
			
				<script>
				
					function InitProc() {
					
						var  oBrw = new UTabulator("struct-brwstr");												
						
						if ( oBrw.table.getData().length == 0 ) 
							return null 					
					
						if ( !confirm( 'Continue?') )
							return false
					
						MsgLoading( 'Init process...', null, 'fas fa-circle-notch fa-spin')
					
						return true
					}
					
				</script>
			
			ENDTEXT 			

		ENDFORM o

	INIT WEB oWeb RETURN
?>

